import java.text.SimpleDateFormat

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'io.fabric'
apply plugin: 'testfairy'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("HH:mm yyyy-MM-dd")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.smartstudio.fbclm"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        manifestPlaceholders = [
                fabricApiKey: fabric_api_key
        ]


        ext.betaDistributionGroupAliasesFilePath = fabric_testers_file

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(release_store_file)
            storePassword release_store_key
            keyAlias release_alias
            keyPassword release_key
        }

        debug {
            storeFile file('keystores/fbclm-debug.keystore')
            storePassword 'fbclmdebug'
            keyAlias 'Fbclm debug'
            keyPassword 'fbclmdebug'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            testCoverageEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix " ${gitSha()} ${buildTime()}"
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    testfairyConfig {
        apiKey testfairy_api_key
        videoRate "1"
        videoQuality "medium"
        iconWatermark true
        maxDuration "10m"
        iconWatermark true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:support-annotations:20.0.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'io.reactivex:rxjava:1.0.9'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
        transitive = true;
    }
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.5-beta'
}

preBuild.doFirst {
    Properties gradleProperties = new Properties()
    gradleProperties.load(new FileInputStream(rootProject.file("gradle.properties")))

    def fabricApiSecret = gradleProperties.getProperty("fabric_api_secret")
    Properties fabricProperties = new Properties()
    File fabricPropertiesFile = new File(project.projectDir, "fabric.properties")
    fabricPropertiesFile.createNewFile()
    fabricProperties.load(new FileInputStream(fabricPropertiesFile))
    fabricProperties.setProperty("apiSecret", fabricApiSecret)
    fabricProperties.store(new FileOutputStream(fabricPropertiesFile), null)
}
